/-
Copyright (c) 2026 CompPoly Contributors. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Valerii Huhnin
-/
module

public import CompPolyBench.Common
public import CompPoly.Bivariate.Basic
public import CompPoly.Fields.BN254

/-!
# Bivariate Benchmarks
-/

public section

open CompPoly

namespace CompPolyBench

/-- Benchmark group metadata for `CompPoly.Bivariate.Basic`. -/
def bivariateGroupInfos : List BenchGroupInfo := [
  ⟨"bivariate-full-koalabear", "Bivariate full evaluation (KoalaBear)"⟩,
  ⟨"bivariate-full-goldilocks", "Bivariate full evaluation (Goldilocks)"⟩,
  ⟨"bivariate-full-bn254", "Bivariate full evaluation (BN254)"⟩
]

/-- Shared input-shape label for bivariate evaluation benchmarks. -/
private def bivariateInputShape : String :=
  "xDegree<8, yDegree<64, one nonzero per 4 coeffs, 32 points"

/-- Build a bivariate polynomial from generated coefficients. -/
private def buildCBivariate {R : Type*}
    [Semiring R] [BEq R] [LawfulBEq R] [Nontrivial R] [DecidableEq R]
    (terms : Array R) : CBivariate R :=
  Id.run do
    let mut p : CBivariate R := 0
    for i in [0:terms.size] do
      let xDegree := i % 8
      let yDegree := i / 8
      p := p + CBivariate.monomialXY xDegree yDegree (terms.getD i 0)
    pure p

/-- Run bivariate full-evaluation benchmarks over a generic prime `ZMod` field. -/
private def runBivariateZMod (modulus : Nat) [Fact (Nat.Prime modulus)]
    (key nameSuffix fieldName fieldTitle : String)
    (largeHornerYxMeasured mediumHornerYxMeasured smallHornerYxMeasured : Nat)
    (largeHornerXyMeasured mediumHornerXyMeasured smallHornerXyMeasured : Nat)
    (preset : BenchPreset) (gen : StdGen) :
    IO (BenchGroup × StdGen) := do
  let (terms, gen) := (zmodArray modulus 512 true).run gen
  let (points, gen) := (zmodArray modulus 64 false).run gen
  let poly := buildCBivariate terms
  let evalPoint (i : Nat) : ZMod modulus × ZMod modulus :=
    let offset := 2 * (i % 32)
    (points.getD (offset % points.size) 0, points.getD ((offset + 1) % points.size) 0)
  let warmup := warmupIterations preset
  let measured := measuredIterations preset
  let hornerYxMeasured :=
    preset.selectNat largeHornerYxMeasured mediumHornerYxMeasured smallHornerYxMeasured
  let hornerXyMeasured :=
    preset.selectNat largeHornerXyMeasured mediumHornerXyMeasured smallHornerXyMeasured
  let checksumIterations := groupChecksumIterations measured [
    hornerYxMeasured, hornerXyMeasured
  ]
  let naive ← runTimed
    ("bivariate-full-eval-naive" ++ nameSuffix) "CBivariate" "evalEval" fieldName
    bivariateInputShape preset warmup measured
    (fun i ↦
      let point := evalPoint i
      CBivariate.evalEval point.1 point.2 poly)
    checksumZMod (checksumIterations := checksumIterations)
  let hornerYx ← runTimed
    ("bivariate-full-eval-horner-yx" ++ nameSuffix) "CBivariate" "evalEvalHornerYThenX"
    fieldName bivariateInputShape preset warmup hornerYxMeasured
    (fun i ↦
      let point := evalPoint i
      CBivariate.evalEvalHornerYThenX point.1 point.2 poly)
    checksumZMod (checksumIterations := checksumIterations)
  let hornerXy ← runTimed
    ("bivariate-full-eval-horner-xy" ++ nameSuffix) "CBivariate" "evalEvalHornerXThenY"
    fieldName bivariateInputShape preset warmup hornerXyMeasured
    (fun i ↦
      let point := evalPoint i
      CBivariate.evalEvalHornerXThenY point.1 point.2 poly)
    checksumZMod (checksumIterations := checksumIterations)
  pure ({
      groupKey := key,
      title := "Bivariate full evaluation (" ++ fieldTitle ++ ")",
      records := #[naive, hornerYx, hornerXy] }, gen)

/-- Run the KoalaBear bivariate full-evaluation benchmark. -/
private def runKoalaBearBivariate (preset : BenchPreset) (gen : StdGen) :
    IO (BenchGroup × StdGen) := do
  let (terms, gen) := (koalaBearArray 512 true).run gen
  let (points, gen) := (koalaBearPoints 64).run gen
  let p := buildCBivariate terms
  let evalPoint (i : Nat) : KoalaBear.Field × KoalaBear.Field :=
    let offset := 2 * (i % 32)
    (points.getD (offset % points.size) 0, points.getD ((offset + 1) % points.size) 0)
  let fastTerms := koalaBearFastArray terms
  let fastPoints := koalaBearFastArray points
  let fastP := buildCBivariate fastTerms
  let fastEvalPoint (i : Nat) : KoalaBear.Fast.Field × KoalaBear.Fast.Field :=
    let offset := 2 * (i % 32)
    (fastPoints.getD (offset % fastPoints.size) 0,
      fastPoints.getD ((offset + 1) % fastPoints.size) 0)
  let warmup := warmupIterations preset
  let measured := measuredIterations preset
  let hornerYxMeasured := preset.selectNat 11000 1600 300
  let hornerXyMeasured := preset.selectNat 100000 14000 3000
  let fastMeasured := preset.selectNat 14000 2000 400
  let fastHornerYxMeasured := preset.selectNat 35000 5000 1000
  let fastHornerXyMeasured := preset.selectNat 1680000 240000 48000
  let checksumIterations := groupChecksumIterations measured [
    hornerYxMeasured, hornerXyMeasured, fastMeasured, fastHornerYxMeasured,
    fastHornerXyMeasured
  ]
  let naive ← runTimed
    "bivariate-full-eval-naive" "CBivariate" "evalEval" "KoalaBear.Field"
    bivariateInputShape preset warmup measured
    (fun i ↦
      let point := evalPoint i
      CBivariate.evalEval point.1 point.2 p)
    checksumKoalaBear (checksumIterations := checksumIterations)
  let fastNaive ← runTimed
    "bivariate-full-eval-naive-fast" "CBivariate" "evalEval" "KoalaBear.Fast.Field"
    bivariateInputShape preset warmup fastMeasured
    (fun i ↦
      let point := fastEvalPoint i
      CBivariate.evalEval point.1 point.2 fastP)
    checksumKoalaBearFast (checksumIterations := checksumIterations)
  let hornerYx ← runTimed
    "bivariate-full-eval-horner-yx" "CBivariate" "evalEvalHornerYThenX" "KoalaBear.Field"
    bivariateInputShape preset warmup hornerYxMeasured
    (fun i ↦
      let point := evalPoint i
      CBivariate.evalEvalHornerYThenX point.1 point.2 p)
    checksumKoalaBear (checksumIterations := checksumIterations)
  let fastHornerYx ← runTimed
    "bivariate-full-eval-horner-yx-fast" "CBivariate" "evalEvalHornerYThenX"
    "KoalaBear.Fast.Field"
    bivariateInputShape preset warmup fastHornerYxMeasured
    (fun i ↦
      let point := fastEvalPoint i
      CBivariate.evalEvalHornerYThenX point.1 point.2 fastP)
    checksumKoalaBearFast (checksumIterations := checksumIterations)
  let hornerXy ← runTimed
    "bivariate-full-eval-horner-xy" "CBivariate" "evalEvalHornerXThenY" "KoalaBear.Field"
    bivariateInputShape preset warmup hornerXyMeasured
    (fun i ↦
      let point := evalPoint i
      CBivariate.evalEvalHornerXThenY point.1 point.2 p)
    checksumKoalaBear (checksumIterations := checksumIterations)
  let fastHornerXy ← runTimed
    "bivariate-full-eval-horner-xy-fast" "CBivariate" "evalEvalHornerXThenY"
    "KoalaBear.Fast.Field"
    bivariateInputShape preset warmup fastHornerXyMeasured
    (fun i ↦
      let point := fastEvalPoint i
      CBivariate.evalEvalHornerXThenY point.1 point.2 fastP)
    checksumKoalaBearFast (checksumIterations := checksumIterations)
  pure ({
    groupKey := "bivariate-full-koalabear",
    title := "Bivariate full evaluation (KoalaBear)",
    records := #[naive, hornerYx, hornerXy, fastNaive, fastHornerYx, fastHornerXy]
  }, gen)

/-- Run the Goldilocks bivariate full-evaluation benchmark. -/
private def runGoldilocksBivariate (preset : BenchPreset) (gen : StdGen) :
    IO (BenchGroup × StdGen) := do
  runBivariateZMod
    Goldilocks.fieldSize "bivariate-full-goldilocks" "-goldilocks" "Goldilocks.Field"
    "Goldilocks" 12000 1700 350 30000 4500 900 preset gen

/-- Run the BN254 bivariate full-evaluation benchmark. -/
private def runBn254Bivariate (preset : BenchPreset) (gen : StdGen) :
    IO (BenchGroup × StdGen) := do
  runBivariateZMod
    BN254.scalarFieldSize "bivariate-full-bn254" "-bn254" "BN254.ScalarField" "BN254"
    12000 1700 350 27000 4000 800 preset gen

/-- Runnable bivariate benchmark tasks. -/
def bivariateTasks : List BenchTask := [
  BenchTask.fromGroupRunner
    ⟨"bivariate-full-koalabear", "Bivariate full evaluation (KoalaBear)"⟩
    runKoalaBearBivariate,
  BenchTask.fromGroupRunner
    ⟨"bivariate-full-goldilocks", "Bivariate full evaluation (Goldilocks)"⟩
    runGoldilocksBivariate,
  BenchTask.fromGroupRunner
    ⟨"bivariate-full-bn254", "Bivariate full evaluation (BN254)"⟩
    runBn254Bivariate
]

/-- Run selected bivariate full-evaluation benchmarks. -/
def runBivariate (preset : BenchPreset) (selection : BenchSelection) (gen : StdGen) :
    IO (Array BenchGroup × StdGen) := do
  runSelectedTasks bivariateTasks preset selection gen

end CompPolyBench
